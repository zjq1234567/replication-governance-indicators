/***************************************************************************/
// Program to estimate Unobserved Components Model (UCM) using data from representative sources 
// Called from:  estimate1.do 
// Input data: repdata -- contains representative sources, for one indicator and year
// Ouput data: repdata -- adds to input data estimate of governance based on representative
// 						sources, as well as estimated parameters for the representative sources.
//						This dataset is then used by the program nonrepcode1.do to combine with
//						data from non-representative sources
// Codes called:  None
/***************************************************************************/

clear
set more off
use repdata

// Save global $varnames containing list of WGI variable names 
qui describe, varlist
global varnames=r(varlist) 
scalar k=r(k) /* save scalar with number of variables */
scalar n=r(N) /* save number of observations */

// Generate means and standard deviations
foreach var of varlist $varnames{
	egen `var'mean=mean(`var')
	egen `var'sd=sd(`var')
	}

// Generate pairwise correlation matrix of data, corrmat 
// Need to do this with double loop, since Stata's pwcorr command does not save entire pairwise correlation matrix 
matrix corrmat=J(k,k,0) 
matrix rownames corrmat = $varnames
matrix colnames corrmat = $varnames
scalar i=1
scalar j=1
foreach vari of varlist $varnames{
	foreach varj of varlist $varnames{
		corr `vari' `varj'
		matrix corrmat[i,j]=r(rho)
		scalar j=j+1
		}
	scalar j=1
	scalar i=i+1
}

// Call Stata factor command which can be used (with correct transformation of results) to estimate Unobserved Components Model
// Note that number of observations specificed in n(.) is immaterial 
// Use ML option which is consistent with normality assumption in Unobserved Components Model 
// After estimating factor model retrieve vector of factor loadings in L 
// Then save as variables, in format `'sourcename'l 
factormat corrmat, n(100) factors(1) ml
matrix L=e(L) /* Matrix with factor loadings */
foreach var in $varnames{
	matrix xx=L["`var'",.]
	svmat xx, names("xxx")
	egen `var'load=mean(xxx)
	drop xxx
	}

// Retrieve parameters of Unobserved Components Model from factor loadings plus means and standard deviations of data 
// Note that factor model in Stata presumes data have zero mean, unit standard deviation, and are generated by 
// y(k)=lambda(k) x factor + sqrt(1-lambda(k)^2)) x u(k) where factor and u(k) both are standard normal 
// With this formulation factormat retrieves factor loadings equal to lambda(k) 
// WGI version of Unobserved Components Model instead assumes y(k)=alpha(k)+beta(k)x(g+eps(k)) 
foreach var in $varnames{
	gen `var'alpha=`var'mean
	gen `var'beta=`var'load*`var'sd
	gen `var'sigma=sqrt((`var'sd/`var'beta)^2-1)
}


// Construct estimates of governance given parameters and data 
gen estgovrep=0 	/* Estimate of governance */
gen sdgovrep=0 		/* Standard error of estimate of governance */
foreach var of varlist $varnames{
	replace estgovrep=estgovrep+(`var'sigma^(-2))*(`var'-`var'alpha)/`var'beta if `var'~=.
	replace sdgovrep=sdgovrep+`var'sigma^(-2) if `var'~=.
	}
egen nsrcrep=rownonmiss($varnames)
replace estgovrep=. if nsrcrep==0
replace sdgovrep=. if nsrcrep==0
replace nsrcrep=. if nsrcrep==0
replace estgovrep=estgovrep/(1+sdgovrep)
replace sdgovrep=1/sqrt(1+sdgovrep)

// Keep dataset with representative source data and estimates of governance based on representative sources
egen cid=seq() // merge key
save repdata, replace

/***************************************************************************/
// End of Code
/***************************************************************************/
